include "./code/io/File.code"

define Expression = struct {
    dump: func (self: Expression) -> Nothing
    evaluate: func (self: Expression) -> Int = null
}

define Int_Expression = struct {
    extends Expression(dump = dump_int, evaluate = evaluate_int)
    value: Int
}

define dump_int = func (self: Expression) -> Nothing {
    let self = self as Int_Expression
    stdout.write(self.value)
}

define evaluate_int = func (self: Expression) -> Int {
    let self = self as Int_Expression
    return self.value
}

define Binary_Expression = struct {
    extends Expression(dump = dump_binary_expression)
    operator: String
    left_expression: Expression
    right_expression: Expression
}

define dump_binary_expression = func (self: Expression) -> Nothing {
    let self = self as Binary_Expression
    stdout.write('(')
    self.left_expression.dump()
    stdout.write(' ').write(self.operator).write(' ')
    self.right_expression.dump()
    stdout.write(')')
}

define Addition_Expression = struct {
    extends Binary_Expression(operator = "+", evaluate = evaluate_addition)
}

define evaluate_addition = func (self: Expression) -> Int {
    let self = self as Addition_Expression
    return self.left_expression.evaluate() + self.right_expression.evaluate()
}

define Substraction_Expression = struct {
    extends Binary_Expression(operator = "-", evaluate = evaluate_substraction)
}

define evaluate_substraction = func (self: Expression) -> Int {
    let self = self as Substraction_Expression
    return self.left_expression.evaluate() - self.right_expression.evaluate()
}

define main = func () -> Nothing {
    let one = new Int_Expression(value = 1)

    let expression: Expression = new Addition_Expression(
        left_expression = new Int_Expression(
            value = 2
        )
        right_expression = new Int_Expression(
            value = 3
        )
    )
    while (expression.evaluate() > 0) {
        expression = new Substraction_Expression(
            left_expression = expression
            right_expression = one
        )
    }

    expression.dump()
    stdout.write(" = ").write(expression.evaluate()).end_line()
}
