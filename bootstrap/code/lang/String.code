\ Copyright (c) 2020, Stefan Selariu

String :: struct {
    data: @Int8
    data_size: Int
    length: Int
}

init :: (self: @String) -> @String {
    self.init(16)
    return self
}

init :: (self: @String, data_size: Int) -> @String {
    self.data_size = data_size
    self.data = malloc(data_size) as @Int8
    self.length = 0
    return self
}

init :: (self: @String, data: @Int8) -> @String {
    data_length := 0
    loop {
        char := data[data_length]
        if (char == '\0') {
            break
        }
        data_length = data_length + 1
    }

    self.length = data_length
    self.data_size = self.length + 1
    self.data = data
    return self
}

clone :: (self: @String) -> @String {
    clone := new String
    clone.init(self.data_size)
    clone.append(self)
    return clone
}

equals :: (self: @String, other: @String) -> Boolean {
    if (self.length == other.length) {
        index := 0
        while (index < self.length) {
            if (self.data[index] != other.data[index]) {
                return false
            }
            index = index + 1
        }
        return true
    }
    return false
}

index_of :: (self: @String, other: @String) -> Int {
    self_index := 0
    while (self_index <= self.length - other.length) {
        other_index := 0
        while (other_index < other.length && self.data[self_index + other_index] == other.data[other_index]) {
            other_index = other_index + 1
        }
        if (other_index == other.length) {
            return self_index
        }
        self_index = self_index + 1
    }
    return -1
}

starts_with :: (self: @String, other: @String) -> Boolean {
    if (self.length < other.length) {
        return false
    }
    index := 0
    while (index < other.length) {
        if (self.data[index] != other.data[index]) {
            return false
        }
        index = index + 1
    }
    return true
}

ends_with :: (self: @String, other: @String) -> Boolean {
    if (self.length < other.length) {
        return false
    }
    index := 0
    while (index < other.length) {
        if (self.data[self.length - other.length + index] != other.data[index]) {
            return false
        }
        index = index + 1
    }
    return true
}

last_index_of :: (self: @String, char: Int8) -> Int {
    return self.last_index_of(char, self.length - 1)
}

last_index_of :: (self: @String, char: Int8, from_index: Int) -> Int {
    index := from_index
    while (index >= 0 && self.data[index] != char) {
        index = index - 1
    }
    return index
}

append :: (self: @String, char: Int8) -> @String {
    if (self.length + 1 >= self.data_size) {
        self.data_size = self.data_size + 16
        self.data = realloc(self.data, self.data_size) as @Int8
    }
    self.data[self.length] = char
    self.length = self.length + 1
    self.data[self.length] = '\0'
    return self
}

append :: (self: @String, number: Int) -> @String {
    if (number < 0) {
        return self.append('-').append(-number)
    }
    if (number >= 10) {
        self.append(number / 10)
    }
    return self.append((number // 10 + '0' as Int) as Int8)
}

append :: (self: @String, other: @String) -> @String {
    if (self.length + other.length >= self.data_size) {
        self.data_size = self.length + other.length + 1
        self.data = realloc(self.data, self.data_size) as @Int8
    }
    index := 0
    other_data := other.data
    self_data := self.data
    self_length := self.length
    loop {
        char := other_data[index]
        self_data[self_length] = char
        if (char == '\0') {
            break
        }
        self_length = self_length + 1
        index = index + 1
    }
    self.length = self_length
    return self
}

remove :: (self: @String, begin_index: Int, end_index: Int) -> @String {
    if (begin_index < 0 || begin_index > end_index || end_index >= self.length) {
        abort("Invalid state")
    }
    index := 0
    while (index + end_index <= self.length) {
        self.data[begin_index + index] = self.data[end_index + index]
        index = index + 1
    }
    self.length = begin_index + index - 1
    return self
}
