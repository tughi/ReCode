\ Copyright (c) 2020-2021, 2023, Stefan Selariu

include "code/utils/Math.code"

struct Rect {
    left = 0i32
    top = 0i32
    right = 0i32
    bottom = 0i32
}

func width(self: @Rect) -> i32 {
    return self.right - self.left + 1
}

func height(self: @Rect) -> i32 {
    return self.bottom - self.top + 1
}

func contains(self: @Rect, x: i32, y: i32) -> bool {
    return self.left <= x and x <= self.right and self.top <= y and y <= self.bottom
}

func set(self: @Rect, left: i32, top: i32, right: i32, bottom: i32) -> Nothing {
    self.left = left
    self.top = top
    self.right = right
    self.bottom = bottom
}

func set(self: @Rect, other: @Rect) -> Nothing {
    self.set(other.left, other.top, other.right, other.bottom)
}

func set_location(self: @Rect, left: i32, top: i32) -> Nothing {
    self.right = self.right - self.left + left
    self.bottom = self.bottom - self.top + top
    self.left = left
    self.top = top
}

func set_size(self: @Rect, width: i32, height: i32) -> Nothing {
    if width < 1 {
        self.right = self.left
    } else {
        self.right = self.left + width - 1
    }
    if height < 1 {
        self.bottom = self.top
    } else {
        self.bottom = self.top + height - 1
    }
}

func intersect(self: @Rect, other: @Rect) -> Nothing {
    let new_left = max(self.left, other.left)
    let new_right = min(self.right, other.right)
    if new_left > new_right {
        new_right = new_left
    }
    let new_top = max(self.top, other.top)
    let new_bottom = min(self.bottom, other.bottom)
    if new_top > new_bottom {
        new_bottom = new_top
    }
    self.set(new_left, new_top, new_right, new_bottom)
}

func intersects(self: @Rect, other: @Rect) -> bool {
    if self.left > other.right or self.top > other.bottom or self.right < other.left or self.bottom < other.top {
        return false
    }
    return true
}

func write(file: @FILE, rect: @Rect) -> @FILE {
    return file.write("[(").write(rect.left).write(", ").write(rect.top).write(") x (").write(rect.right).write(", ").write(rect.bottom).write(")]")
}
