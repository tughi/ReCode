\ Copyright (c) 2020-2021, 2023, Stefan Selariu

include "./LibC.code"

let font: Font

struct Font {
    height: i32
    base_line: i32
    glyphs: [Glyph; 256]
}

struct Glyph {
    width: i32
    mask: [[u8; 9]; 18]
}

func init_font() -> Nothing {
    let file = open_file("editor/Font.data", "r")
    let line = make String()

    if (!(file.read_line(@line) && line.starts_with("\\ Copyright ") && file.read_line(@line) && line.equals(""))) {
        abort()
    }

    if (!(file.read_line(@line) && line.starts_with("font.height: 18"))) {
        abort()
    }
    font.height = 18

    if (!(file.read_line(@line) && line.starts_with("font.base_line: 13"))) {
        abort()
    }
    font.base_line = 13

    let index = 0isize
    while (index < font.glyphs.length) {
        font.glyphs[index].width = 9
        index = index + 1
    }

    while (file.read_line(@line)) {
        if (!line.equals("")) {
            abort()
        }

        if (!file.read_line(@line)) {
            break
        }

        if (!line.starts_with("glyph: ")) {
            abort()
        }
        let glyph = font.glyphs[line.data[line.length - 1]]

        if (!(file.read_line(@line) && line.equals("glyph.width: 9"))) {
            abort()
        }
        glyph.width = 9

        let glyph_row = 0
        while (glyph_row < font.height && file.read_line(@line)) {
            if (line.length != 18usize) {
                abort()
            }

            let glyph_column = 0
            while (glyph_column < glyph.width) {
                if (line.data[glyph_column * 2] == 'F' as u8 && line.data[glyph_column * 2 + 1] == 'F' as u8) {
                    glyph.mask[glyph_row][glyph_column] = 0xffu8
                } else if (line.data[glyph_column * 2] == '-' as u8 && line.data[glyph_column * 2 + 1] == '-' as u8) {
                    \ glyph.mask[glyph_row][glyph_column] = 0x00u8
                } else {
                    abort()
                }
                glyph_column = glyph_column + 1
            }

            glyph_row = glyph_row + 1
        }
    }

    file.close()
}
