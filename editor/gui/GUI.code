\ Copyright (c) 2021, Stefan Selariu

include "./Button.code"
include "./Event.code"
include "./Horizontal_Linear_Layout.code"
include "./Vertical_Linear_Layout.code"

define GUI = struct {
    extends GUI_Layout(
        name = "GUI"
        layout = func (self: GUI_Widget, left: i32, top: i32, right: i32, bottom: i32) -> Nothing {
            let self = self as GUI

            self.if_size_changes(left, top, right, bottom) {
                let width_spec = Measure_Spec(mode = MEASURE_SPEC_MODE_EXACTLY, size = right - left + 1)
                let height_spec = Measure_Spec(mode = MEASURE_SPEC_MODE_EXACTLY, size = bottom - top + 1)
                self.for_each_child() {
                    child.measure(@width_spec, @height_spec)
                }
            }

            self.layout_widget(left, top, right, bottom)
            self.for_each_child() {
                child.layout(left, top, right, bottom)
            }
        }
    )
    hovered_widget: GUI_Widget
    pressed_widget: GUI_Widget = null
}

define init_gui = func () -> GUI {
    let gui = new GUI(gui = undefined, parent = null, hovered_widget = undefined)
    gui.gui = gui
    gui.hovered_widget = gui
    return gui
}

define handle_mouse_moved_event = func (self: GUI, event: GUI_Event_Mouse_Moved) -> Nothing {
    let hovered_widget = self.hovered_widget
    self.update_hovered_widget(event.x, event.y)
}

define handle_mouse_button_pressed = func (self: GUI, event: GUI_Event_Mouse_Button_Pressed) -> Nothing {
    self.pressed_widget = self.hovered_widget
}

define handle_mouse_button_released = func (self: GUI, event: GUI_Event_Mouse_Button_Released) -> Nothing {
    if (self.hovered_widget == self.pressed_widget) {
        self.hovered_widget.handle_click_event()
    }
    self.pressed_widget = null
}
