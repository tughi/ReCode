\ Copyright (c) 2021, Stefan Selariu

include "./Layout.code"

define GUI_Horizontal_Linear_Layout = struct {
    extends GUI_Layout(
        name = "GUI_Horizontal_Linear_Layout"
        measure = measure_horizontal_linear_layout
        layout = layout_horizontal_linear_layout
    )
    margin: i32 = 0
    spacing: i32 = 0
}

define measure_horizontal_linear_layout = func (self: GUI_Widget, width_spec: Measure_Spec, height_spec: Measure_Spec) -> Nothing {
    let self = self as GUI_Horizontal_Linear_Layout

    if (width_spec.mode == MEASURE_SPEC_MODE_EXACTLY) {
        self.width_hint = width_spec.size
    } else {
        self.abort("width", width_spec)
    }

    if (height_spec.mode == MEASURE_SPEC_MODE_EXACTLY) {
        self.height_hint = height_spec.size
    } else {
        self.abort("height", height_spec)
    }

    \ stdout.write(self.name).write(" size hint: ").write(self.width_hint).write(" x ").write(self.height_hint).end_line()
}

define layout_horizontal_linear_layout = func (self: GUI_Widget, left: i32, top: i32, right: i32, bottom: i32) -> Nothing {
    let self = self as GUI_Horizontal_Linear_Layout

    self.bounds.set(left, top, right, bottom)

    if (self.children_count > 0) {
        let stretched_width = self.bounds.width - self.margin * 2 - self.spacing * (self.children_count - 1)
        let stretched_children = 0
        self.for_each_child() {
            if (child.width_policy.mode == SIZE_POLICY_MODE_FIXED) {
                stretched_width = stretched_width - child.width_policy.size
            } else if (child.width_policy.mode == SIZE_POLICY_MODE_WRAP_CONTENT) {
                let child_width_spec = make !Measure_Spec(mode = MEASURE_SPEC_MODE_UNLIMITED)
                let child_height_spec = make !Measure_Spec(mode = MEASURE_SPEC_MODE_LIMITED, size = self.bounds.height - self.margin * 2)
                child.measure(@child_width_spec, @child_height_spec)
                stretched_width = stretched_width - child.width_hint
            } else {
                stretched_children = stretched_children + 1
            }
        }

        let child_left = left + self.margin
        let child_top = top + self.margin
        self.for_each_child() {
            let child_right: i32
            if (child.width_policy.mode == SIZE_POLICY_MODE_FIXED) {
                child_right = child_left + child.width_policy.size - 1
            } else if (child.width_policy.mode == SIZE_POLICY_MODE_WRAP_CONTENT) {
                child_right = child_left + child.width_hint - 1
            } else {
                child_right = child_left + stretched_width / stretched_children - 1
            }
            let child_bottom: i32
            if (child.height_policy.mode == SIZE_POLICY_MODE_FIXED) {
                child_bottom = child_top + child.height_policy.size - 1
            } else if (child.height_policy.mode == SIZE_POLICY_MODE_WRAP_CONTENT) {
                child_bottom = child_top + child.height_hint - 1
            } else {
                child_bottom = bottom - self.margin
            }
            child.layout(child_left, child_top, child_right, child_bottom)
            child_left = child_right + 1 + self.spacing
        }
    }
}

define horizontal_linear_layout = macro (parent: GUI_Layout, content: macro (gui: GUI_Horizontal_Linear_Layout)) {
    let linear_layout = make GUI_Horizontal_Linear_Layout(gui = parent.gui, parent = parent)
    parent.add(linear_layout)
    linear_layout.content()
}
