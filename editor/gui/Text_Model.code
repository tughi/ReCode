\ Copyright (c) 2021, 2023, Stefan Selariu

include "../String.code"

struct GUI_Text_Model {
    first_line: @GUI_Text_Model_Line = null
    last_line: @GUI_Text_Model_Line = null
    lines = 0
}

struct GUI_Text_Model_Line {
    text = make @String()
    next_line: @GUI_Text_Model_Line = null
}

func create_text_model(text: @String) -> @GUI_Text_Model {
    let model = make @GUI_Text_Model()

    let line_text = "".clone()
    text.for_each_char() {
        if char == '\n' as u8 {
            model.append(make @GUI_Text_Model_Line(text = line_text))
            line_text = "".clone()
        } else {
            line_text.append(char)
        }
    }
    model.append(make @GUI_Text_Model_Line(text = line_text))

    return model
}

func append(self: @GUI_Text_Model, line: @GUI_Text_Model_Line) -> Nothing {
    if self.first_line == null {
        self.first_line = line
    } else {
        self.last_line.next_line = line
    }
    self.last_line = line
    self.lines = self.lines + 1
}
