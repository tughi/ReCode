\ Copyright (c) 2020, Stefan Selariu

define Canvas = struct {
    pixels: Any
    width: i32
    height: i32
    color: u32 = 0
    clip_rect: !Rect
}

include "./Rect.code"

define set_color = func (self: Canvas, red: u8, green: u8, blue: u8, alpha: u8) -> Nothing {
    self.color = (red as u32 * 256 + green as u32) * 256 + blue as u32
}

define clear = func (self: Canvas) -> Nothing {
    let array = self.pixels as [u64; ?]
    let array_size = self.width * self.height / 2
    let array_index = 0
    let value = self.color as u64 * 0x1_0000_0001u64
    while (array_index < array_size) {
        array[array_index] = value
        array_index = array_index + 1
    }
}

define draw_pixel = func (self: Canvas, x: i32, y: i32) -> Nothing {
    if (self.clip_rect.contains(x, y) == false) {
        return
    }
    let pixels = self.pixels as [u32; ?]
    let offset = self.width * y + x
    pixels[offset] = self.color
}

define draw_line = func (self: Canvas, x1: i32, y1: i32, x2: i32, y2: i32) -> Nothing {
    let dx: i32
    let sx: i32
    if (x1 < x2) {
        dx = x2 - x1
        sx = 1
    } else {
        dx = x1 - x2
        sx = -1
    }
    let dy: i32
    let sy: i32
    if (y1 < y2) {
        dy = y1 - y2
        sy = 1
    } else {
        dy = y2 - y1
        sy = -1
    }
    let err = dx + dy
    let x = x1
    let y = y1
    loop {
        self.draw_pixel(x, y)
        if (x == x2 && y == y2) {
            return
        }
        let err2 = err * 2
        if (err2 >= dy) {
            err = err + dy
            x = x + sx
        }
        if (err2 <= dx) {
            err = err + dx
            y = y + sy
        }
    }
}

define draw_horizontal_line = func (self: Canvas, x1: i32, x2: i32, y: i32) -> Nothing {
    let x = x1
    if (x2 < x1) {
        x = x2
        x2 = x1
        x1 = x
    }
    while (x <= x2) {
        self.draw_pixel(x, y)
        x = x + 1
    }
}

define draw_vertical_line = func (self: Canvas, x: i32, y1: i32, y2: i32) -> Nothing {
    let y = y1
    if (y2 < y1) {
        y1 = y2
        y2 = y
        y = y1
    }
    while (y <= y2) {
        self.draw_pixel(x, y)
        y = y + 1
    }
}

define draw_rect = func (self: Canvas, x1: i32, y1: i32, x2: i32, y2: i32) -> Nothing {
    self.draw_horizontal_line(x1, x2, y1)
    self.draw_horizontal_line(x1, x2, y2)
    self.draw_vertical_line(x1, y1, y2)
    self.draw_vertical_line(x2, y1, y2)
}

define fill_rect = func (self: Canvas, x1: i32, y1: i32, x2: i32, y2: i32) -> Nothing {
    let y = y1
    if (y2 < y1) {
        y1 = y2
        y2 = y
        y = y1
    }
    while (y <= y2) {
        self.draw_horizontal_line(x1, x2, y)
        y = y + 1
    }
}

include "./SDL.code"
