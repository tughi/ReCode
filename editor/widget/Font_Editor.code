\ Copyright (c) 2020-2021, 2023, Stefan Selariu

include "code/widget/Button.code"
include "code/widget/Scrollbar.code"
include "code/widget/Style.code"
include "code/widget/Text_Widget.code"
include "code/widget/Widget_Group.code"
include "editor/widget/Glyph_Chooser.code"
include "editor/widget/Glyph_Editor.code"

func make_font_editor() -> @Widget_Group {
    let font_editor = make @Widget_Group(
        layout = layout_font_editor
        draw = draw_font_editor
    )

    let glyph_editor = make @Glyph_Editor(glyph = font.glyphs['A'])
    font_editor.add(glyph_editor)

    let save_button = make @Save_Button(glyph_editor = @glyph_editor)
    font_editor.add(save_button)

    let glyph_chooser = make_glyph_chooser(glyph_editor)
    font_editor.add(glyph_chooser)

    let text_widget = make @Text_Widget(text = "the quick brown fox jumps over the lazy dog...\nThe Quick Brown Fox Jumps Over The Lazy Dog...\nTHE QUICK BROWN FOX JUMPS OVER THE LAZY DOG...")
    font_editor.add(text_widget)

    let scrollbar = make_horizontal_scrollbar(
        content_size = 42
        visible_size = 13
        visible_from = 24
        step = 3
    )
    font_editor.add(scrollbar)

    return font_editor
}

struct Save_Button: Button(
    label = "Save"
    on_click = func (self: @Save_Button) -> Nothing {
        @font.save()
    }
) {
    glyph_editor: @Glyph_Editor
}

func layout_font_editor(self: @Widget_Group, left: i32, top: i32, right: i32, bottom: i32) -> Nothing {
    self.layout_widget(left, top, right, bottom)

    let glyph_editor = self.children.first_node.data.as(@Glyph_Editor)
    let save_button = self.children.first_node.next_node.data.as(@Button)
    let glyph_chooser = self.children.first_node.next_node.next_node.data.as(@Glyph_Chooser)
    let text_widget = self.children.first_node.next_node.next_node.next_node.data.as(@Text_Widget)
    let scrollbar = self.children.first_node.next_node.next_node.next_node.next_node.data.as(@Scrollbar)

    define MARGIN = 16

    glyph_editor.compute_size_hint()
    glyph_editor.layout(
        left + MARGIN
        top + MARGIN
        left + MARGIN + glyph_editor.width_hint - 1
        top + MARGIN + glyph_editor.height_hint - 1
    )

    save_button.compute_size_hint()
    save_button.layout(
        glyph_editor.left
        glyph_editor.bottom + MARGIN + 1
        glyph_editor.right
        glyph_editor.bottom + MARGIN + save_button.height_hint
    )

    scrollbar.compute_size_hint()
    scrollbar.layout(
        save_button.left
        save_button.bottom + MARGIN + 1
        save_button.right
        save_button.bottom + MARGIN + scrollbar.height_hint
    )

    glyph_chooser.layout(
        glyph_editor.right + MARGIN + 1
        glyph_editor.top
        right - MARGIN
        bottom - MARGIN
    )

    text_widget.layout(
        glyph_chooser.left
        glyph_chooser.children.last().bottom + MARGIN + 1
        glyph_chooser.right
        bottom - MARGIN
    )
}

func draw_font_editor(self: @Widget_Group, canvas: @Canvas) -> Nothing {
    canvas.set_color(COLOR_BACKGROUND)
    canvas.fill_rect(self)

    self.draw_widget_group(canvas)
}
