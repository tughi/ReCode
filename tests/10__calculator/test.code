func main(anon argc: i32, anon argv: [[u8; ?]; ?]) {
    if argc != 2 {
        exit(1)
    }

    let tokenizer = make Tokenizer(
        data: argv[1]
        index: 0
    )

    while tokenizer.has_next_token() {
        let token = tokenizer.next_token()
        if token.kind == 0 {
            break
        }
        stdout.write(token: token).end_line()
    }
}

struct Tokenizer {
    data: [u8; ?]
    index: i32

    func has_next_token(self) -> bool {
        return self.data[self.index] != 0
    }

    func next_token(self) -> @Token {
        let char = self.data[self.index]
        if char >= '0' and char <= '9' {
            return self.scan_number_token()
        }
        self.index = self.index + 1
        return make @Token(
            kind: char
            value: 0
        )
    }

    func scan_number_token(self) -> @Token {
        let value = 0
        loop {
            let char = self.data[self.index]
            if char < '0' or char > '9' {
                break
            }
            value = value * 10 + (char - '0').as(i32)
            self.index = self.index + 1
        }
        return make @Token(
            kind: 'n'
            value: value
        )
    }
}

struct Token {
    kind: u8
    value: i32
}

\ ----------- Stream -----------

func @FILE.write(self, char: u8) -> @FILE {
    fputc(char, stdout)
    return self
}

func @FILE.write(self, signed value: i32) -> @FILE {
    \ TODO: Use a buffer to avoid recursion
    if value < 0 {
        self.write(char: '-')
        return self.write(signed: -value)
    }
    if value >= 10 {
        self.write(signed: value / 10)
    }
    return self.write(char: (value // 10).as(u8) + '0')
}

func @FILE.write(self, token: @Token) -> @FILE {
    self.write(char: token.kind)
    if token.kind == 'n' {
        self.write(char: ':').write(char: ' ').write(signed: token.value)
    }
    return self
}

func @FILE.end_line(self) -> @FILE {
    self.write(char: '\n')
    return self
}

\ ------------ LibC ------------

external type FILE

external stdin: @FILE
external stdout: @FILE
external stderr: @FILE

external func fputc(anon c: u8, anon file: @FILE) -> i32

external func malloc(anon size: u64) -> @Any

external func exit(anon code: i32)
