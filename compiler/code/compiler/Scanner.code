\ Copyright (c) 2020, Stefan Selariu

Scanner :: struct {
    source: @Source
    current_char_index: Int = 0
    current_line: Int = 1
    current_column: Int = 1
    current_token: @Token = undefined
}

create_scanner :: (source: @Source) -> @Scanner {
    scanner := new Scanner(source = source)
    scanner.current_token = scanner.scan_token()
    return scanner
}

peek_token :: (self: @Scanner, offset: Int) -> @Token {
    if (offset < 0) {
        abort("Invalid state")
    }
    token := self.current_token
    loop {
        if (token.next_token == null) {
            token.next_token = self.scan_token()
        }
        token = token.next_token
        if (offset == 0) {
            return token
        }
        offset = offset - 1
    }
}

next_token :: (self: @Scanner) -> @Token {
    if (self.current_token.next_token == null) {
        self.current_token.next_token = self.scan_token()
    }
    self.current_token = self.current_token.next_token
    return self.current_token
}

peek_char :: (self: @Scanner) -> Int8 {
    return self.source.content[self.current_char_index]
}

next_char :: (self: @Scanner) -> Int8 {
    next_char := self.source.content[self.current_char_index]
    if (next_char != '\0') {
        self.current_char_index = self.current_char_index + 1
        if (next_char == '\n') {
            self.current_line = self.current_line + 1
            self.current_column = 1
        } else {
            self.current_column = self.current_column + 1
        }
    }
    return next_char
}

scan_token :: (self: @Scanner) -> @Token {
    token_lexeme := new String()
    token_location := new Source_Location(source = self.source, line = self.current_line, column = self.current_column)

    next_char := self.peek_char()
    if (next_char.is_identifier_start()) {
        return self.scan_identifier_token(token_lexeme, token_location)
    }
    if (next_char.is_digit()) {
        return self.scan_integer_token(token_lexeme, token_location)
    }
    if (next_char == '\'') {
        return self.scan_character_token(token_lexeme, token_location)
    }
    if (next_char == '"') {
        return self.scan_string_token(token_lexeme, token_location)
    }
    if (next_char == '\\') {
        return self.scan_comment_token(token_lexeme, token_location)
    }
    if (next_char.is_space()) {
        return self.scan_space_token(token_lexeme, token_location)
    }
    if (next_char == '\n') {
        self.next_char()
        return new End_Of_Line_Token(lexeme = token_lexeme, location = token_location)
    }
    if (next_char == '\0') {
        self.next_char()
        return new End_Of_File_Token(lexeme = token_lexeme, location = token_location)
    }
    token_lexeme.append(self.next_char())
    return new Other_Token(lexeme = token_lexeme, location = token_location)
}

is_digit :: (char: Int8) -> Boolean {
    return char as Int >= '0' as Int && char as Int <= '9' as Int
}

is_identifier_start :: (char: Int8) -> Boolean {
    return char.is_letter() || char == '_'
}

is_identifier_body :: (char: Int8) -> Boolean {
    if (char.is_identifier_start()) {
        return true
    }
    return char.is_digit()
}

is_letter :: (char: Int8) -> Boolean {
    return (char as Int >= 'a' as Int && char as Int <= 'z' as Int) || (char as Int >= 'A' as Int && char as Int <= 'Z' as Int)
}

is_space :: (char: Int8) -> Boolean {
    return char == ' '
}

scan_character_token :: (self: @Scanner, token_lexeme: @String, token_location: @Source_Location) -> @Token {
    if (self.peek_char() != '\'') {
        abort("Invalid state")
    }

    value: Int8
    token_lexeme.append(self.next_char())
    char := self.next_char()
    token_lexeme.append(char)
    if (char == '\'') {
        return new Error_Token(lexeme = token_lexeme, location = token_location)
    }
    if (char == '\\') {
        char = self.next_char()
        token_lexeme.append(char)
        if (char.is_escape() == false) {
            return new Error_Token(lexeme = token_lexeme, location = token_location)
        }
        if (char == 'n') {
            value = '\n'
        } else if (char == '\"') {
            value = char
        } else if (char == '\'') {
            value = char
        } else if (char == '\\') {
            value = char
        } else if (char == 't') {
            value = '\t'
        } else if (char == '0') {
            value = '\0'
        } else {
            abort("Invalid state")
        }
        char = self.next_char()
        token_lexeme.append(char)
        if (char != '\'') {
            return new Error_Token(lexeme = token_lexeme, location = token_location)
        }
    } else {
        if (char == '\0' || char == '\n' || char == '\t') {
            return new Error_Token(lexeme = token_lexeme, location = token_location)
        }
        value = char
        char = self.next_char()
        token_lexeme.append(char)
        if (char != '\'') {
            return new Error_Token(lexeme = token_lexeme, location = token_location)
        }
    }
    return new Character_Token(lexeme = token_lexeme, location = token_location, value = value)
}

is_escape :: (char: Int8) -> Boolean {
    return char == 'n' || char == 't' || char == '\"' || char == '\'' || char == '\\' || char == '0'
}

scan_comment_token :: (self: @Scanner, token_lexeme: @String, token_location: @Source_Location) -> @Token {
    while (self.peek_char() != '\n') {
        token_lexeme.append(self.next_char())
    }
    return new Comment_Token(lexeme = token_lexeme, location = token_location)
}

scan_identifier_token :: (self: @Scanner, token_lexeme: @String, token_location: @Source_Location) -> @Token {
    while (self.peek_char().is_identifier_body()) {
        token_lexeme.append(self.next_char())
    }
    if (token_lexeme.equals("as")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("break")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("else")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("external")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("false")) {
        return new Boolean_Token(lexeme = token_lexeme, location = token_location, value = false)
    }
    if (token_lexeme.equals("if")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("include")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("loop")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("new")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("null")) {
        return new Null_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("return")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("struct")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("true")) {
        return new Boolean_Token(lexeme = token_lexeme, location = token_location, value = true)
    }
    if (token_lexeme.equals("undefined")) {
        return new Undefined_Token(lexeme = token_lexeme, location = token_location)
    }
    if (token_lexeme.equals("while")) {
        return new Keyword_Token(lexeme = token_lexeme, location = token_location)
    }
    return new Identifier_Token(lexeme = token_lexeme, location = token_location)
}

scan_integer_token :: (self: @Scanner, token_lexeme: @String, token_location: @Source_Location) -> @Token {
    value := 0
    while (self.peek_char().is_digit()) {
        char := self.next_char()
        value = value * 10 + (char as Int - '0' as Int)
        token_lexeme.append(char)
    }
    return new Integer_Token(lexeme = token_lexeme, location = token_location, value = value)
}

scan_space_token :: (self: @Scanner, token_lexeme: @String, token_location: @Source_Location) -> @Token {
    count := 0
    while (self.peek_char() == ' ') {
        char := self.next_char()
        count = count + 1
        token_lexeme.append(char)
    }
    return new Space_Token(lexeme = token_lexeme, location = token_location, count = count)
}

scan_string_token :: (self: @Scanner, token_lexeme: @String, token_location: @Source_Location) -> @Token {
    if (self.peek_char() != '"') {
        abort("Invalid state")
    }

    value := new String()

    token_lexeme.append(self.next_char())
    loop {
        char := self.peek_char()
        if (char == '\0' || char == '\n') {
            return new Error_Token(lexeme = token_lexeme, location = token_location)
        }
        char = self.next_char()
        token_lexeme.append(char)
        if (char == '"') {
            return new String_Token(lexeme = token_lexeme, location = token_location, value = value)
        }
        if (char == '\\') {
            char = self.peek_char()
            if (char == '\0' || char == '\n') {
                return new Error_Token(lexeme = token_lexeme, location = token_location)
            }
            char = self.next_char()
            token_lexeme.append(char)
            if (char.is_escape() == false) {
                return new Error_Token(lexeme = token_lexeme, location = token_location)
            }
            if (char == 'n') {
                value.append('\n')
            } else if (char == '\"') {
                value.append(char)
            } else if (char == '\'') {
                value.append(char)
            } else if (char == '\\') {
                value.append(char)
            } else if (char == 't') {
                value.append('\t')
            } else if (char == '0') {
                value.append('\0')
            } else {
                abort("Invalid state")
            }
        } else {
            value.append(char)
        }
    }
}
